plugins {
    id 'net.neoforged.moddev' version '2.0.88'
}

repositories {
    mavenLocal()
    mavenCentral()
    maven { url 'https://maven.minecraftschurli.at/maven-public' }
    maven { url 'https://maven.blamejared.com/' }
    maven { url 'https://cursemaven.com' }
}

sourceSets.main.resources {
    srcDir 'src/generated/resources'
}

dependencies {
    // Uncomment next line for local testing
    //implementation files("libs/bibliocraft-${mc_version}-${bc_version}.jar")

    compileOnly "com.github.minecraftschurlimods:bibliocraft:${mc_version}-${bc_version}:api"
    runtimeOnly "com.github.minecraftschurlimods:bibliocraft:${mc_version}-${bc_version}"
    compileOnly "mezz.jei:jei-${mc_version}-neoforge-api:${jei_version}"
    runtimeOnly "mezz.jei:jei-${mc_version}-neoforge:${jei_version}"

    // Aether
    implementation "curse.maven:aether-255308:6389857"
    implementation "curse.maven:owo-lib-532610:6416633"
    // Allthemodium
    implementation "curse.maven:allthemodium-364466:6389532"
    // Ars Nouveau
    implementation "curse.maven:ars-nouveau-401955:6407266"
    implementation "curse.maven:curios-309927:6401872"
    implementation "curse.maven:geckolib-388172:6304958"
    // Butchercraft
    implementation "curse.maven:butchercraft-265715:6058964"
    // Cobblemon
    implementation "curse.maven:cobblemon-687131:6125026"
    implementation "curse.maven:kotlin-for-forge-351264:5978393"
    // Deep Aether
    implementation "curse.maven:deep-aether-852465:6221136"
    // Deeper and Darker
    implementation "curse.maven:deeperdarker-659011:5908863"
    // Eternal Starlight
    implementation "curse.maven:eternal-starlight-1080592:6383693"
    // Expanded Delight
    implementation "curse.maven:expanded-delight-620770:6125094"
    implementation "curse.maven:farmers-delight-398521:6154807"
    // Extra Delight
    implementation "curse.maven:extradelight-842106:6362163"
    // Forbidden Arcanus
    implementation "curse.maven:forbidden-arcanus-309858:6331424"
    implementation "curse.maven:valhelsia-core-416935:6296775"
    // GregTech
    implementation "curse.maven:gregtechceu-modern-890405:5812613"
    // Integrated Dynamics
    implementation "curse.maven:integrated-dynamics-236307:6418767"
    implementation "curse.maven:common-capabilities-247007:6384335"
    implementation "curse.maven:cyclops-core-232758:6414678"
    // My Nether's Delight
    implementation "curse.maven:my-nethers-delight-1003673:5977439"
    // Occultism
    implementation "curse.maven:occultism-361026:6355135"
    implementation "curse.maven:modonomicon-538392:6338537"
    implementation "curse.maven:smartbrainlib-661293:6101242"
    // Silent Gear
    implementation "curse.maven:silent-gear-297039:6401304"
    implementation "curse.maven:silent-lib-242998:6285150"
    // Twilight Forest
    implementation "curse.maven:the-twilight-forest-227639:6384738"
    // Undergarden
    implementation "curse.maven:the-undergarden-379849:5769852"
    // Vampirism
    implementation "curse.maven:vampirism-become-a-vampire-233029:6387585"
    // Werewolves
    implementation "curse.maven:werewolves-become-a-beast-417851:6164806"
}

neoForge {
    version = project.neo_version

    mods {
        "${mod_id}" {
            sourceSet sourceSets.main
        }
    }

    runs {
        configureEach {
            logLevel = org.slf4j.event.Level.DEBUG
        }
        client {
            client()
        }
        data {
            data()
            programArguments.addAll '--mod', "${mod_id}", '--all',
                    '--output',       file('src/generated/resources/').getAbsolutePath(),
                    '--existing',     file('src/main/resources/').getAbsolutePath(),
                    '--existing-mod', 'bibliocraft',
                    '--existing-mod', 'aether',
                    '--existing-mod', 'allthemodium',
                    '--existing-mod', 'ars_nouveau',
                    '--existing-mod', 'butchercraft',
                    '--existing-mod', 'cobblemon',
                    '--existing-mod', 'deep_aether',
                    '--existing-mod', 'deeperdarker',
                    '--existing-mod', 'eternal_starlight',
                    '--existing-mod', 'expandeddelight',
                    '--existing-mod', 'extradelight',
                    '--existing-mod', 'forbidden_arcanus',
                    '--existing-mod', 'gtceu',
                    '--existing-mod', 'integrateddynamics',
                    '--existing-mod', 'mynethersdelight',
                    '--existing-mod', 'occultism',
                    '--existing-mod', 'silentgear',
                    '--existing-mod', 'twilightforest',
                    '--existing-mod', 'undergarden',
                    '--existing-mod', 'vampirism',
                    '--existing-mod', 'werewolves'
        }
        server {
            server()
            programArgument '--nogui'
        }
    }
}

tasks.named('processResources', ProcessResources) {
    var replaceProperties = [
            mc_version       : mc_version,
            mc_version_range : mc_version_range,
            neo_version      : neo_version,
            neo_version_range: neo_version_range,
            bc_version       : bc_version,
            bc_version_range : bc_version_range,
            loader_version   : loader_version,
            mod_id           : mod_id,
            mod_version      : mod_version,
            mod_name         : mod_name,
            mod_authors      : mod_authors,
            mod_credits      : mod_credits,
            mod_logo         : mod_logo,
            mod_description  : mod_description,
            mod_license      : mod_license,
    ]
    inputs.properties replaceProperties
    filesMatching('META-INF/neoforge.mods.toml') {
        expand replaceProperties
    }
}

jar {
    archiveBaseName = mod_id
    archiveVersion = "${mc_version}-${mod_version}"
    processResources.exclude('**/.cache/**')
}
